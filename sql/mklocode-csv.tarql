PREFIX cc1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes-Adjunct/>
PREFIX cc2: <https://www.omg.org/spec/LCC/Countries/ISO3166-2-SubdivisionCodes-Adjunct/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX lcc-cr: <https://www.omg.org/spec/LCC/Countries/CountryRepresentation/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
PREFIX lcc-3166-1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tempo: <http://purl.org/tempo/>
PREFIX un-loc: <http://data.ga-group.nl/un-locode/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>

CONSTRUCT {
	?loc
		a un-loc:Location ;
		a owl:Individual ;
		lcc-cr:isPartOf ?cc1 ;
		lcc-cr:isPartOf ?cc2 ;
		un-loc:hasStatus ?stat ;
		un-loc:hasFunction ?func ;
		geo:lat ?lat ;
		geo:long ?long ;
		rdfs:label ?Name ;
		rdfs:label ?enName ;
		skos:note ?enRemarks ;
		skos:notation ?LOCODE ;
		tempo:validFrom	?new ;
		tempo:validTill	?rem ;
		dct:isReplacedBy ?rpl ;
		.
}
WHERE {
	BIND(NOW() AS ?now)
	BIND("2021-12-13"^^xsd:date AS ?rdt)
	BIND(IF(?a = "+",?rdt,?nil) AS ?new)
	BIND(IF(?a = "X",?rdt,?nil) AS ?rem)
	BIND(CONCAT(?b,?c) AS ?LOCODE)
	BIND(IRI(CONCAT(STR(cc1:), ?b)) AS ?cc1)
	BIND(IRI(CONCAT(STR(cc2:), ?b,'-',REPLACE(?f," ",""))) AS ?cc2)
	BIND(IRI(CONCAT(STR(un-loc:), 'stat-',?h)) AS ?stat)
	BIND(IRI(CONCAT(STR(un-loc:),IF(STRLEN(?LOCODE) = 5,?LOCODE,?nil))) AS ?loc)
	BIND(REPLACE(REPLACE(REPLACE(?d,
		"  *", " "),
		"\u2019","'"),
		" $", "") AS ?Name)
	BIND(STRLANG(REPLACE(REPLACE(REPLACE(?e,
		"  *", " "),
		"\u2019","'"),
		" $", ""), 'en') AS ?enName)
	BIND(STRLANG(?l, 'en') AS ?enRemarks)
	## snarf replacement from comment
	BIND(IRI(CONCAT(STR(un-loc:), IF(REGEX(?l, "Use .. ..."), REPLACE(?l, "Use (..) (...)", "$1$2"), ?nil))) AS ?rpl)

	?funs apf:strSplit (?g "") .
	VALUES (?funs ?func) {
		("0" undef)
		("1" un-loc:func-Port)
		("2" un-loc:func-RailTerminal)
		("3" un-loc:func-RoadTerminal)
		("4" un-loc:func-Airport)
		("5" un-loc:func-PostalExchangeOffice)
		("6" un-loc:func-Multimodal)
		("7" un-loc:func-FixedTransport)
		("B" un-loc:func-BorderCrossing)
		("-" undef)
	}

	BIND(STRBEFORE(?k, " ") AS ?lats)
	BIND(STRAFTER(?k, " ") AS ?longs)
	BIND(SUBSTR(?lats,5,1) AS ?latsg)
	BIND(SUBSTR(?longs,6,1) AS ?longsg)
	BIND(xsd:decimal(fn:format-number((xsd:double(SUBSTR(?lats,1,2))+xsd:double(SUBSTR(?lats,3,2))/60.0)*IF(?latsg='N',1.0,IF(?latsg='S',-1.0,?null)),"#0.00")) AS ?lat)
	BIND(xsd:decimal(fn:format-number((xsd:double(SUBSTR(?longs,1,3))+xsd:double(SUBSTR(?longs,4,2))/60.0)*IF(?longsg='E',1.0,IF(?longsg='W',-1.0,?null)),"#0.00")) AS ?long)
}
