PREFIX cc1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes-Adjunct/>
PREFIX cc2: <https://www.omg.org/spec/LCC/Countries/ISO3166-2-SubdivisionCodes-Adjunct/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX lcc-cr: <https://www.omg.org/spec/LCC/Countries/CountryRepresentation/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
PREFIX lcc-3166-1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tempo: <http://purl.org/tempo/>
PREFIX un-loc: <http://data.ga-group.nl/un-locode/>
PREFIX uncefact: <https://service.unece.org/trade/uncefact/vocabulary/uncefact#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>

CONSTRUCT {
	?loc
		a uncefact:Location ;
		a owl:Individual ;
		lcc-cr:isPartOf ?cc1 ;
		lcc-cr:isPartOf ?cc2 ;
		un-loc:hasStatus ?stat ;
		un-loc:hasFunction ?func ;
		geo:lat ?lat ;
		geo:long ?long ;
		rdfs:label ?loName ;
		rdfs:label ?enName ;
		skos:note ?enRemarks ;
		skos:notation ?LOCODE ;
		tempo:efficaciousFrom ?idt ;
		.
}
WHERE {
	BIND(NOW() AS ?now)
	BIND(STRAFTER(STR(<>), '!') AS ?param)
	BIND(STRBEFORE(?param, ',') AS ?rev)
	BIND(STRAFTER(?param, ',') AS ?rdt)

	BIND(xsd:gYearMonth(?Date) AS ?idt)
	BIND(CONCAT(?Country,?Location) AS ?LOCODE)
	BIND(IRI(CONCAT(STR(cc1:), ?Country)) AS ?cc1)
	BIND(IRI(CONCAT(STR(cc2:), ?Country,'-',REPLACE(?Subdivision," ",""))) AS ?cc2)
	BIND(IRI(CONCAT(STR(un-loc:), 'stat-',?Status)) AS ?stat)
	BIND(IRI(CONCAT(STR(un-loc:),IF(STRLEN(?LOCODE) = 5,?LOCODE,?nil))) AS ?loc)

	BIND(REPLACE(REPLACE(REPLACE(?Name,
		"  *", " "),
		"\u2019","'"),
		" $", "") AS ?loName)
	BIND(STRLANG(REPLACE(REPLACE(REPLACE(?NameWoDiacritics,
		"  *", " "),
		"\u2019","'"),
		" $", ""), 'en') AS ?enName)
	BIND(STRLANG(?Remarks, 'en') AS ?enRemarks)

	?funs apf:strSplit (?Function "") .
	VALUES (?funs ?func) {
		("0" undef)
		("1" un-loc:func-Port)
		("2" un-loc:func-RailTerminal)
		("3" un-loc:func-RoadTerminal)
		("4" un-loc:func-Airport)
		("5" un-loc:func-PostalExchangeOffice)
		("6" un-loc:func-Multimodal)
		("7" un-loc:func-FixedTransport)
		("B" un-loc:func-BorderCrossing)
		("-" undef)
	}

	BIND(STRBEFORE(?Coordinates, " ") AS ?lats)
	BIND(STRAFTER(?Coordinates, " ") AS ?longs)
	BIND(SUBSTR(?lats,5,1) AS ?latsg)
	BIND(SUBSTR(?longs,6,1) AS ?longsg)
	BIND(xsd:decimal(fn:format-number((xsd:double(SUBSTR(?lats,1,2))+xsd:double(SUBSTR(?lats,3,2))/60.0)*IF(?latsg='N',1.0,IF(?latsg='S',-1.0,?null)),"#0.00")) AS ?lat0)
	BIND(xsd:decimal(fn:format-number((xsd:double(SUBSTR(?longs,1,3))+xsd:double(SUBSTR(?longs,4,2))/60.0)*IF(?longsg='E',1.0,IF(?longsg='W',-1.0,?null)),"#0.00")) AS ?long0)
	BIND(IF(?lat0 != 0 || ?long0 != 0, ?lat0, ?nil) AS ?lat)
	BIND(IF(?lat0 != 0 || ?long0 != 0, ?long0, ?nil) AS ?long)
}
