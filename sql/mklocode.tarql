PREFIX cc1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes-Adjunct/>
PREFIX cc2: <https://www.omg.org/spec/LCC/Countries/ISO3166-2-SubdivisionCodes-Adjunct/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX gn: <https://www.geonames.org/ontology#>
PREFIX lcc-cr: <https://www.omg.org/spec/LCC/Countries/CountryRepresentation/>
PREFIX lcc-lr: <https://www.omg.org/spec/LCC/Languages/LanguageRepresentation/>
PREFIX lcc-3166-1: <https://www.omg.org/spec/LCC/Countries/ISO3166-1-CountryCodes/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tempo: <http://purl.org/tempo/>
PREFIX un-loc: <http://data.ga-group.nl/un-locode/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
	?loc
		a un-loc:Location ;
		a owl:Individual ;
		lcc-cr:isPartOf ?cc1 ;
		lcc-cr:isPartOf ?cc2 ;
		un-loc:hasStatus ?stat ;
		un-loc:hasFunction ?func ;
		rdfs:label ?Name ;
		rdfs:label ?enName .
}
WHERE {
	BIND(NOW() AS ?now)
	BIND(IRI(CONCAT(STR(cc1:), SUBSTR(?LOCODE, 1, 2))) AS ?cc1)
	BIND(IRI(CONCAT(STR(cc2:), SUBSTR(?LOCODE, 1, 2),'-',?SubDiv)) AS ?cc2)
	BIND(IRI(CONCAT(STR(un-loc:), 'stat-',?Status)) AS ?stat)
	BIND(IRI(CONCAT(STR(un-loc:),IF(STRLEN(?LOCODE) = 5,?LOCODE,?nil))) AS ?loc)
	BIND(STRLANG(?NameWoDiacritics, 'en') AS ?enName)
	?funs apf:strSplit (?Function "") .
	VALUES (?funs ?func) {
		("0" undef)
		("1" un-loc:func-Port)
		("2" un-loc:func-RailTerminal)
		("3" un-loc:func-RoadTerminal)
		("4" un-loc:func-Airport)
		("5" un-loc:func-PostalExchangeOffice)
		("6" un-loc:func-Multimodal)
		("7" un-loc:func-FixedTransport)
		("B" un-loc:func-BorderCrossing)
		("-" undef)
	}
}
